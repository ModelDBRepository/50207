/*----------------------------------------------------------------------------

	Kinetic NMDA receptor model
	----------------------------------

	Demo file to show the behaviour of a kinetic NMDA receptor model 
	reproducing the data in figure 2 in Kampa et al (2004) J Physiol.

	The NMDA receptor model uses realistic rates of magnesium block and its 
	effects on channel desensitisation. Presynaptic transmitter release is 
	necessary to for glutamate binding to the receptor. 
 
	Kinetic model from Kampa et al (2004) Kinetics of Mg2+ unblock of NMDA 
	receptors: implications for spike-timing dependent synaptic plasticity. 
	In: J Physiol 556.2: pp 337–345.
	

  	Written by Bjoern Kampa, Canberra, 2004 

----------------------------------------------------------------------------*/


//----------------------------------------------------------------------------
//  general parameters
//----------------------------------------------------------------------------

dt			= 0.025
tstop 		= 500
runStopAt 		= tstop
steps_per_ms 	= 1/dt
celsius 		= 23
v_init 		= -65



//----------------------------------------------------------------------------
//  create compartments and insert passive properties
//----------------------------------------------------------------------------
// presynaptic compartment with transmitter release 
// necessary for glutamate binding to receptor

create PRE,POST
forall {
  diam=10
  L=10
  insert pas
  g_pas=1/5000
  e_pas=v_init
}



//----------------------------------------------------------------------------
//  insert presynaptic mechanisms
//----------------------------------------------------------------------------

access PRE	
insert rel		// glutamate release
del_rel = 10
dur_rel = 1		
amp_rel = 1


//----------------------------------------------------------------------------
//  insert postsynaptic mechansisms
//----------------------------------------------------------------------------

objectvar c
c = new NMDA_Mg()				// create synapse
POST c.loc(0.5)				// assign postsynaptic compartment
setpointer c.C , PRE.T_rel(0.5)	// assign presynaptic compartment
Erev_NMDA_Mg	= 5			// (mV) NMDA reversal potential
mg_NMDA_Mg		= 1			// (mM) put in physiol magnesium

c.gmax 		= 50			// (pS) maximum conductance

//----------------------------------------------------------------------------
//  add graphs
//----------------------------------------------------------------------------

//xopen("nmda.ses")
dur_rel=5000
xopen("nmda_longpulse.ses")
